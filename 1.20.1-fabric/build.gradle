plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
}

platformInfo {
    minecraftVersion = '1.20.1'
    platform = 'Fabric'
    supportedMinecraftVersions = [ '1.20.1' ]
}

loom {
    // Uncomment this to enable the usage of Access Widener
    // accessWidenerPath = file('src/main/resources/examplemod.accesswidener')

    runs {
        client {
            runDir './run-client'
        }

        server {
            runDir './run-server'
        }

        gameTest {
            server()
            vmArg "-Dfabric-api.gametest=1"
        }

        datagen {
            client()
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=examplemod"
            vmArg "-Dfabric-api.datagen.output-dir=${file("./src/generated/resources")}"
        }

        configureEach {
            ideConfigGenerated = true
        }
    }
}

repositories {
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.net/"
    }
    maven {
        // ModMenu
        name = 'TerraformerMC'
        url = 'https://maven.terraformersmc.com/releases/'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:1.20.1"
    mappings loom.layered() {
        //officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:0.16.14"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.92.6+1.20.1"
    api project(":1.20.1-base")

    // Need this to tell me what mods are present at runtime
    modRuntimeOnly(group: 'com.terraformersmc', name: 'modmenu', version: '7.+') {
        exclude group: 'net.fabricmc'
        exclude group: 'net.fabricmc.fabric-api'
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}
