plugins {
    id 'com.modrinth.minotaur' version '2.+' apply false
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.+"
    id "io.github.pacifistmc.forgix" version "2.+"
    id 'project-report'
}

interface PlatformInfoExtension {
    // Which platform (i.e. Fabric/Forge/Quilt) is this implementation is on?
    // In case of projects uses solely VanillaGradle, here we use 'Vanilla'.
    Property<String> getPlatform();
    // Which Minecraft version is this implementation based on?
    Property<String> getMinecraftVersion();
    // Which Minecraft version is this implementation compatible with?
    ListProperty<String> getSupportedMinecraftVersions();
}

def mainVersion = '1.0.3'
def java17Version = ['1.18.2', '1.19.4', '1.20.1'] as Set
def allVersion = ['1.18.2', '1.19.4', '1.20.1', '1.20.4', '1.21.1']

subprojects { Project p ->
    apply plugin: 'java'
    apply plugin: 'com.modrinth.minotaur' // https://stackoverflow.com/a/50153617
    apply plugin: 'project-report'

    var platformInfo = p.extensions.create('platformInfo', PlatformInfoExtension)

    java.toolchain.languageVersion = JavaLanguageVersion.of(platformInfo.getMinecraftVersion().getOrElse("null") in java17Version ? 17 : 21)
    java.withSourcesJar()

    p.archivesBaseName = "ExampleMultiProjectMod"
    p.version = mainVersion

    p.repositories {
        // Enable maven central for all subprojects
        mavenCentral()
        // Enable sponge maven for all subprojects, for access to Mixin
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
            content {
                includeGroup 'org.spongepowered'
            }
        }
    }

    if (!p.name.startsWith('share')) {
        p.dependencies {
            implementation project(':share')
        }
    }

    p.tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
    }

    p.processResources {
        // Exclude .cache directory which is generated by DataGen.
        exclude '.cache'
    }

    p.afterEvaluate { Project pAfter ->

        if (pAfter.name.split('-')[0] in java17Version) {
            //forge minecraftLibrary
            pAfter.dependencies {
                minecraftLibrary(jarJar(implementation("com.github.luben:zstd-jni:1.5.6-9")))
            }
        } else if (p.name.split('-')[0] in allVersion) {
            //neoforge additionalRuntimeClasspath
            pAfter.dependencies {
                additionalRuntimeClasspath(jarJar(implementation("com.github.luben:zstd-jni:1.5.6-9")))
            }
        } else {
            pAfter.dependencies {
                implementation "com.github.luben:zstd-jni:1.5.6-9"
            }
        }

        if (pAfter.name.endsWith('forge')) {
            pAfter.processResources {
                from rootProject.project('share').sourceSets.main.resources
            }
        }

        def projectExt = pAfter.extensions.platformInfo as PlatformInfoExtension

        pAfter.archivesBaseName += "-${projectExt.platform.get()}-${projectExt.minecraftVersion.get()}"

        pAfter.modrinth {
            token = System.getenv("MODRINTH_TOKEN")
            projectId = System.getenv("MODRINTH_PROJECT_ID")
            loaders = [projectExt.platform.get().toLowerCase(Locale.ROOT)]
            gameVersions = projectExt.supportedMinecraftVersions.getOrElse([])
            uploadFile = pAfter.jar
            versionNumber = mainVersion
            versionType = 'release' // alpha | beta | release
            // TODO Declare Dependency
        }
        if ("Vanilla" == projectExt.platform.get()) {
            // Disable modrinth task for base projects
            pAfter.tasks.named("modrinth") {
                enabled = false
            }
        }

        pAfter.jar {
            manifest.attributes([
                    "Specification-Title"   : "NotEnoughBandwidth",
                    "Specification-Vendor"  : "USS_Shenzhou, Burning_TNT",
                    "Specification-Version" : "1.0",
                    "Implementation-Title"  : "${pAfter.archivesBaseName}",
                    "Implementation-Version": "${pAfter.version}",
                    "Implementation-Vendor" : "USS_Shenzhou, Burning_TNT"
            ])
        }
    }

}

allprojects {
    configurations.configureEach {
        resolutionStrategy {
            cacheDynamicVersionsFor 30, 'days'
            cacheChangingModulesFor 30, 'days'
        }
    }
}

forgix {
    silence = false
    autoRun = false
    archiveVersion = mainVersion

    multiversion {
        inputJars = project.files(subprojects
                .findAll { p ->
                    p.name.endsWith('forge')
                }
                .collect { p ->
                    p.tasks.named('jar', Jar).flatMap { t -> t.archiveFile }
                }
        )
    }
}

tasks.named('build') {
    finalizedBy(tasks.named('mergeVersions'))
}